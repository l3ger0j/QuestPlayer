plugins {
    alias(libs.plugins.androidx.room)
    alias(libs.plugins.android.application)
    alias(libs.plugins.hilt.android.gradle.plugin)
}

def viewBind = true
def dataBind = true

room {
    schemaDirectory "$projectDir/schemas"
}

android {
    namespace 'org.qp.android'
    compileSdk 34

    defaultConfig {
        applicationId "org.qp.android"
        minSdk 26
        targetSdk 34
        versionCode 202300
        versionName "3.23.0"
        resourceConfigurations += ['en', 'ru']
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    configurations {
        configureEach {
            exclude group: 'xpp3', module: 'xpp3'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }

    signingConfigs {
        release {
            storeFile file("keystore/github_keystore")
            storePassword System.getenv('KEYSTORE_PASSWORD')
            keyAlias System.getenv('RELEASE_SIGN_KEY_ALIAS')
            keyPassword System.getenv('RELEASE_SIGN_KEY_PASSWORD')
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding viewBind
        dataBinding dataBind
        buildConfig true
        aidl true
    }

    ndkVersion = '26.1.10909125'
    lint {
        abortOnError false
    }

}

dependencies {
    coreLibraryDesugaring(libs.desugar.jdk.libs)

    implementation libs.appcompat
    implementation libs.constraintlayout
    implementation libs.documentfile
    implementation libs.preference
    implementation libs.fragment
    implementation libs.webkit
    implementation libs.navigation.fragment
    implementation libs.androidx.navigation.ui
    implementation libs.androidx.work.runtime
    implementation libs.material
    implementation libs.androidx.core.splashscreen
    implementation libs.androidx.room.runtime
    annotationProcessor libs.androidx.room.compiler

    implementation libs.hilt.android
    annotationProcessor libs.hilt.compiler

    implementation libs.changelog
    implementation libs.colorpicker
    implementation libs.jsoup
    implementation libs.appupdater
    implementation libs.storage
    implementation libs.picasso

    implementation libs.rxandroid

    implementation libs.jackson.core
    implementation libs.jackson.databind
    implementation libs.jackson.annotations

    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}